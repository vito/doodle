import(File expand-path("../broomhlda/lib/hl", _FILE))

module(Doodle::Styles):
  module(Code):
    _highlight(language, source) :=
      pre highlight:
        _highlight-inline(language, source)

    _highlight-inline(language, source) := do:
      lang = string(language)
      code = string(source)

      AutoLinker new(
        HL::Lexers const-get(lang) new(code) run
      ) format-tokens(self)

    auto-link?("n") := true
    auto-link?(_) := false

    class(<< self):
      post-process(document) :=
        Doodle::Builder with(document at-css("head")):
          link(#[
            rel: "stylesheet"
            type: "text/css"
            href: "public/highlight.css"
          ])

      pre-save(_, out) := do:
        public = File expand-path("../public", _FILE)
        Dir glob(public + "/*") each (f):
          FileUtils cp(f, out + "/public")


class(AutoLinker < HL::Formatters::Html):
  define(format-tokens(to)):
    @tokens each (t):
      to span(#[class: t type tag]) (s):
        if(to auto-link?(t type tag) &&
            (url = to doc tag-url(t contents)))
          then:
            s a(#[href: url]): text(t contents)
          else:
            s text(t contents)

    to
