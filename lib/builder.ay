class(Doodle::Builder < Nokogiri::HTML::Builder):
  attr-reader(#styles)

  define(initialize(options = Hash new, root = nil, &block)):
    @styles = [Doodle::Styles::Default]

    @styles each (m):
      extend(m)

    super

  scan(t: Doodle::AST::Tree) :=
    Doodle::AST::Tree new(
      t line
      t nodes collect (n): scan(n)
    )

  scan(c: Doodle::AST::Chunk) := c

  scan(s: Doodle::AST::Send) := do:
    res = s

    @styles each (x):
      when(x respond-to?(#scan)):
        res =! x scan(@doc, [s message to-sym] + s arguments)

    Doodle::AST::Send new(
      s line
      res at(0) to-s
      res drop(1) collect (c):
        Doodle::AST::Node from(c)
    )

  build(t: Doodle::AST::Tree) :=
    t nodes each (n): build(n)

  build(c: Doodle::AST::Chunk) :=
    text(c content)

  build(s: Doodle::AST::Value) :=
    text(s value to-s)

  build(s: Doodle::AST::Send) :=
    if(respond-to?("_" + s message))
      then: send("_" + s message, *(s arguments))
      else:
        raise(
          NoMethodError new(
            "unknown document message `" + s message +
              "' at line " + s line to-s
            s message
            *(s arguments)
          )
        )

  post-process :=
    @doc tap (d):
      @styles each (s):
        when(s respond-to?(#post-process)):
          s post-process(d)

  generate(t, flow? = true, &tweaks) := do:
    @doc down each (d):
      Doodle::Builder with(d) (s):
        s generate(d tree)

    html:
      head:
        title:
          text("Untitled")

      body:
        div content!:
          div section:
            when(tweaks):
              tweaks call(@doc, self)

            build(t)

    when(flow?):
      Doodle::AutoFlow flow(@doc)

    post-process

  class(<< self):
    define(run(t, flow? = true, &tweaks)):
      b = Doodle::Builder new

      scanned = b scan(t)

      b generate(scanned, flow?, &tweaks)

    define(run-sanitize(node)):
      sanitize(run-inline(node))

    define(run-strip(node)):
      strip-tags(run-inline(node))

    define(run-inline(node)):
      b = Doodle::Builder new
      b body: build(node)
      b doc at-css("body") inner-html

    define(inline(&body)):
      b = Doodle::Builder new(&body)
      b doc children at(1)

    strip-tags(str) :=
      Nokogiri::XML::DocumentFragment parse(str) tap (x):
        x traverse (node):
          when(node is-a?(Nokogiri::XML::Element)):
            node replace(
              Nokogiri::XML::Text new(
                node inner-html
                node document
              )
            )

    sanitize(str) :=
      strip-tags(str) to-html
        gsub(r{\s+}, "-")
        gsub(r{[^[:alnum:]_\-:.]}, "")
        downcase
