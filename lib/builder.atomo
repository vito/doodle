class(Anatomy::Builder < Nokogiri::HTML::Builder):
  attr-reader(#markup)

  define(initialize(options = Hash new, root = nil, &block)):
    @markup = Anatomy::Markup new(self)
    @styles = [Anatomy::Styles::Default]

    @styles each (m):
      @markup extend(m)

    super

  build(t: Anatomy::AST::Tree) :=
    t nodes each (n): build(n)

  build(c: Anatomy::AST::Chunk) :=
    text(c content)

  build(s: Anatomy::AST::Send) :=
    if(@markup respond-to?(s message))
      then: @markup send(s message, *(s arguments))
      else:
        raise(
          NoMethodError new(
            "unknown document message `" + s message +
              "' at line " + s line to-s
            s message
            *(s arguments)
          )
        )

  apply-styles :=
    @doc tap (d):
      @styles each (s):
        s apply(d)

  class(<< self):
    define(run(t, flow? = true, &tweaks)):
      b = Anatomy::Builder new

      b html:
          head:
            title:
              text("Untitled")

          body:
            div content!:
              div section:
                when(tweaks):
                  tweaks call(b doc, b)

                build(t)

      when(flow?):
        Anatomy::AutoFlow flow(b doc)

      b apply-styles

    define(run-sanitize(node)):
      sanitize(run-inline(node))

    define(run-strip(node)):
      strip-tags(run-inline(node))

    define(run-inline(node)):
      b = Anatomy::Builder new
      b body: build(node)
      b doc at-css("body") inner-html

    strip-tags(str) :=
      Nokogiri::XML::DocumentFragment parse(str) tap (x):
        x traverse (node):
          when(node is-a?(Nokogiri::XML::Element)):
            node replace(
              Nokogiri::XML::Text new(
                node inner-html
                node document
              )
            )

    sanitize(str) :=
      strip-tags(str) to-html
        gsub(r{[A-Z.]}, ".\\0")
        gsub(r{[^[:alnum:]_\-:.]}, "_")
