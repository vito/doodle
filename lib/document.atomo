class(Anatomy::Document < Nokogiri::HTML::Document):
  attr-accessor(#title, #tag, #tags, #styles, #up, #down, #tree)

  define(initialize(@title = nil, @tag = "", @tree = nil)):
    super

    @tags = Hash new
    @up = nil
    @down = []

  top? := @up nil?

  define(depth):
    d = 0
    p = self

    until(p top?):
      d += 1
      p =! p up

    d

  url :=
    if(top?)
      then: @tag + ".html"
      else: @up url sub(r{#.*$}, "") + "#section_" + @tag

  save-to(where) := do:
    when(top?):
      File open(where + "/" + @tag + ".html", "w")
           write(to-html)

    @down each (s):
      s save-to(where)

  define(search-tags):
    title = @title
    tag = @tag

    search-title = Anatomy::Builder run-strip(@title)
    url = url

    tags = [
      [ search-title
        Anatomy::Builder inline:
          span title:
            a(#[href: url]): build(title)
        url
      ]
      [ @tag
        Anatomy::Builder inline:
          span tag:
            text("tag: ")
            code:
              a(#[href: url]): text(tag)
        url
      ]
    ]

    @down each (d):
      sts = d search-tags

      sts each (t):
        unless(t at(1) at-css(".parent")):
          Anatomy::Builder with(t at(1)):
            span parent:
              text(" in ")
              a(#[href: url]): build(title)

      tags += sts

    tags
