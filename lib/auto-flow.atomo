class(Anatomy::AutoFlow):
  : self ; run(body) :=
    new(body) flow to-html

  define(initialize(body: String)):
    @document = Nokogiri::HTML::DocumentFragment parse(body)

  define(initialize(@document)):
    #ok

  flow?(x) := x description inline?
  flow?(x: Nokogiri::XML::Text) :=
    !(paragraphs?(x content))

  split?(_) := false
  split?(x: Nokogiri::XML::Text) :=
    paragraphs?(x content)

  empty?(p) :=
    p children empty? || p children all? (p):
      p match:
        Nokogiri::XML::Text ->
          p content strip empty?

        _ ->
          false

  paragraphs?(str) := str include?("\n\n")

  define(flow):
    para = nil
    @document children each (c):
      condition:
        -- flowable; add to current paragraph
        flow?(c) && para -> do:
          c parent = para

        -- flowable; start a new paragraph
        flow?(c) -> do:
          para =! Nokogiri::XML::Element new("p", @document)
          c parent = para

        -- chunk of text finishing current paragraph and
        -- containing more
        split?(c) && para -> do:
          ended? = false
          c content each-line("") (p):
            para << p sub(r{\n*$}, "")
            c before(para)
            para =! Nokogiri::XML::Element new("p", @document)
            ended? =! p suffix?("\n\n")

          when(ended?):
            para =! nil

          c remove

        -- chunk of text containing multiple paragraphs
        split?(c) -> do:
          ended? = false
          c content each-line("") (p):
            para =! Nokogiri::XML::Element new("p", @document)
            para << p sub(r{\n*$}, "")
            c before(para)
            ended? =! p suffix?("\n\n")

          if(ended?)
            then: para =! nil
            else:
              -- remove the last one as we'll still be adding to it
              c previous-sibling remove

          c remove

        -- hit a block element; recurse through it
        para -> do:
          unless(empty?(para)):
            c before(para)

          para =! nil

          Anatomy::AutoFlow run(c)

    -- ended while still adding para; finish him!
    when(para && !(empty?(para))):
      @document << para

    @document
